10 REM FIND MEAN AND STANDARD DEVIATION, APR 25, 1982
11 REM IDENTIFIERS
13 REM	M1%	MAX%	MAXIMUM LENGTH FOR X
14 REM  M2 	MEAN	MEAN OF VECTOR X
15 REM  S1	STD	STANDARD DEVIATION
16 REM	S3 	SUMX	SUM OF X
17 REM 	S4	SUMSQ	SUM OF X SQUARED
18 REM	END OF IDENTIFIERS
19 REM
20 M1% = 80
30 DIM X(80)
40 PRINT "CALCULATION OF MEAN AND STANDARD DEVIATION"
50 GOSUB 500 : REM INPUT DATA
60 GOSUB 9000 : REM FIND MEAN AND STANDARD DEVIATION
70 PRINT
80 PRINT " FOR " ; N% ; " POINTS, MEAN = ";M2;
90 PRINT " SIGMA = "; S1
100 GOTO 50
500 REM INPUT THE DATA
510 INPUT " HOW MANY POINTS";N%
515 IF (N% > M1%) THEN 510
520 IF (N% < 2) THEN 9999
530 FOR I = 1 TO N%
540 PRINT I;
550 INPUT X(I)
560 NEXT I
570 RETURN : REM FROM INPUT
9000 REM FIND MEAN AND STADARD DEVIATION OF N POINTS IN ARRAY X
9001 REM IDENTIFIERS
9004 REM	M2	MEAN	MEAN OF VECTOR X
9006 REM	S1	STD	STANDARD DEVIATION
9007 REM	S3	SUMX	SUM OF X
9008 REM	S4	SUMSQ   SUM OF X SQUARED
9009 REM END OF IDENTIFIERS
9010 REM
9020 S3 = 0
9030 S4 = 0
9040 FOR I = 1 TO N%
9050 S3 = S3 + X(I)
9060 S4 = S4 + X(I)*X(I)
9070 NEXT I
9080 M2 = S3/N%
9090 S1 = SQR((S4 - S3 * S3 / N%)/(N% -1 ))
9100 RETURN : REM FROM MEAN AND STANDARD DEVIATION
9999 END
